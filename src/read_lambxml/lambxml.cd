(********************************************************************************)
(* Attributes.									*)
(********************************************************************************)

type Stdargs = {order =? String; label =? String; extra =? String}
type Linkargs = Stdargs + {href = String}


(********************************************************************************)
(* Top-level element.								*)
(********************************************************************************)

type Document = <document>[ Super_block* ]


(********************************************************************************)
(* Blocks.									*)
(********************************************************************************)

type Super_block =
	  Top_block
	| Nestable_block

type Top_block =
	  Heading_block
	| <title (Stdargs)>[ Super_node+ ]
	| <subtitle (Stdargs)>[ Super_node+ ]
	| <abstract (Stdargs)>[ Paragraph_block+ ]
	| <rule (Stdargs)>[]

type Heading_block =
	  <part (Stdargs)>[ Super_node+ ]
	| <appendix (Stdargs)>[]
	| <section (Stdargs)>[ Super_node+ ]
	| <subsection (Stdargs)>[ Super_node+ ]
	| <subsubsection (Stdargs)>[ Super_node+ ]
	| <bibliography (Stdargs)>[]
	| <notes (Stdargs)>[]
	| <toc (Stdargs)>[]

type Nestable_block =
	  Paragraph_block
	| Itemize_block
	| Enumerate_block
	| Quote_block
	| Mathtex_block
	| Mathml_block
	| Code_block
	| Verbatim_block
	| Tabular_block
	| Bitmap_block
	| Subpage_block
	| <equation (Stdargs)>[ Equation_block Caption_block ]
	| <printout (Stdargs)>[ Printout_block Caption_block ]
	| <table (Stdargs)>[ Table_block Caption_block ]
	| <figure (Stdargs)>[ Figure_block Caption_block ]
	| <bib (Stdargs)>[ Bib_title_block Bib_author_block Bib_resource_block ]
	| <note (Stdargs)>[ Nestable_block+ ]

type Item = <li (Stdargs)>[ Nestable_block+ ]


(********************************************************************************)
(* Definition of individual blocks.						*)
(********************************************************************************)

type Caption_block = <caption (Stdargs)>[ Super_node+ ]
type Paragraph_block = <p (Stdargs)>[ Super_node+ ]
type Itemize_block = <ul (Stdargs)>[ Item+ ]
type Enumerate_block = <ol (Stdargs)>[ Item+ ]
type Quote_block = <quote (Stdargs)>[ Nestable_block+ ]
type Mathtex_block = <mathtex (Stdargs)>[ PCDATA ]
type Mathml_block = <mathml (Stdargs)>[ AnyXml ]
type Code_block = <code (Stdargs + {lang = String})>[ PCDATA ]
type Verbatim_block = <pre (Stdargs)>[ PCDATA ]
type Tabular_block = <tabular (Stdargs + {cols = String})> Tabular
type Bitmap_block = <bitmap (Stdargs)>[ PCDATA ]
type Subpage_block = <subpage (Stdargs)>[ Super_block+ ]
type Bib_title_block = <what (Stdargs)>[ Super_node+ ]
type Bib_author_block = <who (Stdargs)>[ Super_node+ ]
type Bib_resource_block = <where (Stdargs)>[ Super_node+ ]


(********************************************************************************)
(* Definition of floaters.							*)
(********************************************************************************)

type Equation_block = Mathtex_block | Mathml_block
type Printout_block = Code_block
type Table_block = Tabular_block
type Figure_block = Bitmap_block | Verbatim_block | Subpage_block


(********************************************************************************)
(* Rules for tabular environment.						*)
(********************************************************************************)

type Tabular = [ Head? Body+ Foot? | Row+ Body* Foot? ]

type Head = <head>[]
type Body = <body>[]
type Foot = <foot>[]
type Row = <row>[]


(********************************************************************************)
(* Nodes.									*)
(********************************************************************************)

type Nonlink_node =
	  Char
	| <mathtex (Stdargs)>[PCDATA]
	| <mathml (Stdargs)>[PCDATA]
	| <bold (Stdargs)>[ Super_node+ ]
	| <emph (Stdargs)>[ Super_node+ ]
	| <mono (Stdargs)>[ Super_node+ ]
	| <caps (Stdargs)>[ Super_node+ ]
	| <thru (Stdargs)>[ Super_node+ ]
	| <sup (Stdargs)>[ Super_node+ ]
	| <sub (Stdargs)>[ Super_node+ ]
	| <mbox (Stdargs)>[ Super_node+ ]

type Link_node =
	  <link (Linkargs)>[ Nonlink_node+ ]
	| <see (Linkargs)>[]
	| <cite (Linkargs)>[]
	| <ref (Linkargs)>[]
	| <sref (Linkargs)>[]
	| <mref (Linkargs)>[ Nonlink_node+ ]

type Super_node = Nonlink_node | Link_node

